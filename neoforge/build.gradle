plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'

}

base {
    archivesName = "${mod_id}-${neoforge_mod_version}-${minecraft_version}-neoforge"
}

dependencies {
    // This is still required to add the library in your jar and at compile time.
    jarJar(implementation("org.apache.commons:commons-rng-sampling:1.6")) {}
    jarJar(implementation("org.apache.commons:commons-rng-client-api:1.6")) {}

    additionalRuntimeClasspath "org.apache.commons:commons-rng-sampling:1.6"
    additionalRuntimeClasspath "org.apache.commons:commons-rng-client-api:1.6"
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }